//rtl_format
{ MSElang Copyright (c) 2018 by Martin Schreiber

    See the file COPYING.MSE, included in this distribution,
    for details about the copyright.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
}
unit rtl_format;
interface
uses
 __mla__internaltypes;

procedure format(const fmt: string8; const args: array of const): string8;

implementation
uses
 __mla__compilerunit,rtl_flotostr,rtl_sysexceptions,rtl_sysconsts,
 rtl_stringconv;

procedure format(const fmt: string8; const args: array of const): string8;
type
 argsty = record
  high: int32;
  data: pvarrecty;
 end;
 pargsty = ^argsty;

 convertinfoty = record
  ps,pe,pd: pchar8;
  pbuf: pchar8;
  value: int32;
  valindex: int32;
  width,precision: int32;
  minus: bool1;
  s: pointer; //string8, string16 or string32
  pb: pchar8;
  neg: bool1;
  args: pargsty;
 end;

 procedure formaterror();
 begin
  // TO FIX
  // raise econverterror.createfmt(sinvalidformat,[fmt]);
  //  writeln(sinvalidformat); // fred
 end; //formaterror()

 procedure converterror(const itemindex: int32; const wantedtype: string8);
 begin
  // TO FIX
  // raise econverterror.createfmt(stypeexpected,[itemindex,wantedtype]);
  // writeln(stypeexpected); // fred
 end; //converterror();

 procedure rangeerror(const itemindex: int32);
 begin
  // TO FIX
  // raise econverterror.createfmt(srangeerror,[itemindex]);
  // writeln(srangeerror); // fred
 end; //rangeerror();
 
 procedure getnum(var info: convertinfoty): bool1;
 var
  ps: pchar8;
 begin
  info.value:= 0;
  ps:= info.ps;
  result:= false;
  if ps^ = '*' then
   inc(ps);
   if info.valindex > info.args^.high then
    formaterror();
   else
    if (args[info.valindex].vtype = vt_int32) then
     info.value:= args[info.valindex].vint32;
     inc(info.valindex);
     result:= true;
    else
     converterror(info.valindex,'integer');
    end;
   end;
  else
   if (ps^ >= '0') and (ps^ <= '9') then
    result:= true;
    while ps < info.pe do
     if (ps^ >= '0') and (ps^ <= '9') then
      info.value:= info.value*10 + ord(ps^)-ord('0');
      inc(ps);
     else
      break;
     end;
    end;
   end;
  end;
  info.ps:= ps;
 end; //getnum()

var
 info: convertinfoty; 
 buffer: array[0..255] of char8;

 procedure pushbufferreverse(var info: convertinfoty);
 var
  i1,i2,i3: int32;
  pd,ps,pe: pchar8;
 begin
  i2:= info.pd - pchar8(pointer(result)); //current length
  i1:= info.pbuf - pchar8(@buffer);       //additional text
  if i1 < info.width then
   i3:= i2 + info.width;                  //new length
  else
   i3:= i2 + i1;                          //new length
  end;
  i3:= i3 + (info.pe - info.ps);          //new max length
  if i3 > length(result) then
   setlength(result,i3 + i3 div 2);         //1.5 * new length
  end;
  ps:= info.pbuf - 1;
  pd:= @pchar8(pointer(result))[i2];
  if not info.minus then
   pe:= pd + info.width - i1;
   while pd < pe do
    pd^:= ' ';
    inc(pd);
   end;
  end;
  pe:= pd + i1;
  while pd < pe do
   pd^:= ps^;
   dec(ps);
   inc(pd);
  end;
  if info.minus then
   pe:= pd + info.width - i1;
   while pd < pe do
    pd^:= ' ';
    inc(pd);
   end;
  end;
  info.pd:= pd;
 end; //pushbufferreverse()

 procedure pushnum(var info: convertinfoty);
 var
  p1,pe1: pchar8;
 begin
  with info do
   p1:= pb;
   pe1:= p1 + precision - (p1 - pchar8(@buffer));
   while p1 < pe1 do
    p1^:= '0';
    inc(p1);
   end;
   if neg then
    p1^:= '-';
    inc(p1);
   end;
   info.pbuf:= p1;
   pushbufferreverse(info);
  end;
 end; //pushnum()

 procedure pushstring32(var info: convertinfoty);
 var
  s1: string8;
  i1,i2,i4,i5: int32;
  padding: int32;
  pd,ps,pe: pchar8;
 begin
  i2:= info.pd - pchar8(pointer(result)); //current length
  i1:= length(string32(info.s));
  if (info.precision >= 0) and (info.precision < i1) then
   i1:= info.precision;
  end;
  s1:= string32to8(info.s,i1);
  padding:= info.width - i1; //padding
  if padding < 0 then
   padding:= 0;
  end;
  i4:= length(s1);
  i5:= i2 + i4 + (info.pe - info.ps); //new max length
  if i5 > length(result) then
   pd:= pointer(result);
   setlength(result,i5+i5 div 2);
   info.pd:= info.pd + (pchar8(pointer(result)) - pd);
  end;
  ps:= pointer(s1);
  pd:= @pchar8(pointer(result))[i2];
  if not info.minus then
   pe:= pd + padding;
   while pd < pe do
    pd^:= ' ';
    inc(pd);
   end;
  end;
  pe:= pd + length(s1);
  while pd < pe do
   pd^:= ps^;
   inc(ps);
   inc(pd);
  end;
  if info.minus then
   pe:= pd + padding;
   while pd < pe do
    pd^:= ' ';
    inc(pd);
   end;
  end;
  info.pd:= pd;
 end; //pushstring32()

 procedure pushstring8(var info: convertinfoty);
 var
  s1: string32;
 begin
  s1:= string32(string8(info.s));
  info.s:= pointer(s1);
  pushstring32(info);
 end; //pushstring8()

 procedure pushstring16(var info: convertinfoty);
 var
  s1: string32;
 begin
  s1:= string32(string16(info.s));
  info.s:= pointer(s1);
  pushstring32(info);
 end; //pushstring16()

 procedure pushstring16(var info: convertinfoty; const avalue: string16);
 var
  s1: string32;
 begin
  s1:= string32(avalue);
  info.s:= pointer(s1);
  pushstring32(info);
 end; //pushstring16()

var
 itemindex: int32;

 procedure getflo(out avalue: flo64): boolean;
 begin
  result:= true;
  with args[itemindex] do
   case vtype of
    vt_flo64:
     avalue:= vflo64^;
    vt_int32:
     avalue:= vint32;
    vt_card32:
     avalue:= vcard32;
    vt_int64:
     avalue:= vint64^;
    vt_card64:
     avalue:= vcard64^;
    else:
     result:= false;
     converterror(itemindex,'float');
   end;
  end;
 end;
 
var
 p1,pe1: pchar8;
 i1: int32;
 i641: int64;
 c1: card32;
 c641: card64;
 b1: boolean;
 f1: flo64;
 decimalsep: char8;
 thousandsep: char8;
  
begin
 decimalsep:= '.'; //todo: use decimalsettings
 thousandsep:= '''';
 setlength(result,length(fmt));
 info.args:= @args;

 with info do
  ps:= pointer(fmt);
  pe:= ps + length(fmt);
  pd:= pointer(result);
  valindex:= 0;
  while ps < pe do
   if ps^ = '%' then
    inc(ps);
    case ps^ of 
     '%':
      pd^:= ps^;
      inc(pd);
     else:
      minus:= false;
      width:= -1;
      precision:= -1;
      itemindex:= valindex;
      b1:= getnum(info);
      if ps^ = ':' then
       valindex:= value; //default 0
       itemindex:= value;
       inc(ps);
       b1:= false;       //consumed
       inc(valindex);    //next value
      else
       itemindex:= valindex;
       inc(valindex);
      end;
      if ps^ = '-' then
       minus:= true;
       inc(ps);
       if b1 then
        formaterror();
       end;
      end;
      if b1 or getnum(info) then
       width:= value;
      end;
      if ps^ = '.' then
       inc(ps);
       if not getnum(info) then
        formaterror();
       end;
       precision:= value;
      end;
      if itemindex > high(format.args) then
       formaterror();
      end;
      p1:= @buffer;
      with format.args[itemindex] do 
       case lowerchars[ps^] of
        'd':
         case vtype of
          vt_int32,vt_card32:
           i1:= vint32;
           if vtype = vt_card32 then
            i1:= vcard32;
            if i1 < 0 then
             rangeerror(itemindex);
            end;
           end;
           neg:= i1 < 0;
           if neg then
            i1:= -i1;
           end;
           repeat
            p1^:= char8(card8(i1 mod 10)+ord('0'));
            i1:= i1 div 10;
            inc(p1);
           until i1 = 0;
          vt_int64,vt_card64:
           i641:= vint64^;
           if vtype = vt_card64 then
            i641:= vcard64^;
            if i641 < 0 then
             rangeerror(itemindex);
            end;
           end;
           neg:= i641 < 0;
           if neg then
            i641:= -i641;
           end;
           repeat
            p1^:= char8(card8(i641 mod 10)+ord('0'));
            i641:= i641 div 10;
            inc(p1);
           until i641 = 0;
          else:
           converterror(itemindex,'integer');
         end;
         pb:= p1;
         pushnum(info);
        'u':
         case vtype of
          vt_int32,vt_card32:
           c1:= vcard32;
           if vtype = vt_card32 then
            c1:= vint32;
            if vint32 < 0 then
             rangeerror(itemindex);
            end;
           end;
           neg:= false;
           repeat
            p1^:= char8(card8(c1 mod 10)+ord('0'));
            c1:= c1 div 10;
            inc(p1);
           until c1 = 0;
          vt_int64,vt_card64:
           c641:= vcard64^;
           if vtype = vt_int64 then
            c641:= vint64^;
            if vint64^ < 0 then
             rangeerror(itemindex);
            end;
           end;
           neg:= false;
           repeat
            p1^:= char8(card8(c641 mod 10)+ord('0'));
            c641:= c641 div 10;
            inc(p1);
           until c641 = 0;
          else:
           converterror(itemindex,'cardinal');
         end;
         pb:= p1;
         pushnum(info);
        'e':
          if getflo(f1) then
           i1:= info.precision;
           if i1 < 0 then
            i1:= 14;
           else
            i1:= i1-1
           end;
           info.precision:= -1;
           pushstring16(info,flo64tostring16(f1,i1,fsm_sci,decimalsep));
          end;
        'f':
          if getflo(f1) then
           i1:= info.precision;
           if i1 < 0 then
            i1:= 0;
           end;
           info.precision:= -1;
           pushstring16(info,flo64tostring16(f1,i1,fsm_fix,decimalsep));
          end;
        'n':
          if getflo(f1) then
           i1:= info.precision;
           if i1 < 0 then
            i1:= 0;
           end;
           info.precision:= -1;
           pushstring16(info,flo64tostring16(f1,i1,fsm_fix,decimalsep,
                                                                thousandsep));
          end;
        'g':
          if getflo(f1) then
           i1:= info.precision;
           if i1 < 0 then
            i1:= 0;
           end;
           info.precision:= -1;
           pushstring16(info,flo64tostring16(f1,i1,fsm_fix,decimalsep));
          end;
        's':
         case vtype of
          vt_string8:
           info.s:= vstring8;
           pushstring8(info);
          vt_string16:
           info.s:= vstring16;
           pushstring16(info);
          vt_string32:
           info.s:= vstring32;
           pushstring32(info);
          else:
           converterror(itemindex,'string');
         end;
       end;
      end;
    end;
   else
    pd^:= ps^;
    inc(pd);
   end;
   inc(ps);
  end;
  setlength(result,pd-pchar8(pointer(result)));
 end;
end;

end.
